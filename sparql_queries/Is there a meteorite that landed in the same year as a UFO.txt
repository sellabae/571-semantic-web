#Is there a meteorite that landed in a year that had 3 ufo Sightings in the same location?

PREFIX owl:<http://www.w3.org/2002/07/owl#>
PREFIX time:<http://www.w3.org/2006/time#>
PREFIX rdf:<http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX solar: <http://example.org/solar>
PREFIX lunar: <http://example.org/lunar>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX prt: <http://webprotege.stanford.edu/>
SELECT DISTINCT ?meteor ?metLat ?metLong
WHERE{
    {
        #Find years that have recorded 3 UFO sightings
        SELECT ?year(count(?year) as ?yearCount)
        WHERE
        {
            ?ufo a prt:ufoSighting.
            ?ufo time:year ?year.  
        } GROUP BY ?year HAVING(?yearCount = 3)
    }
    
    #Which ufo sightings were in that year and what were their locations?
    {?ufo a prt:ufoSighting;
         time:year ?year;
         geo:lat ?ufoLat;
         geo:long ?ufoLong.
    }.       
    #Which meteor's were found in that year and what were their 		locations?
    ?meteor rdf:type ?type.
    ?type rdfs:subClassOf* prt:meteorite.
    ?meteor time:year ?year.
    ?meteor geo:lat ?metLat.
    ?meteor geo:long ?metLong.
#    {?meteor a prt:meteorite;
#                geo:lat ?metLat;
#                time:year ?year;
#                geo:long ?metLong.
#    }.
    # Filter meteors that are within +/- latitude and longitude of UFO
    FILTER(?ufoLat - 10 < ?metLat && ?metLat < ?ufoLat + 10 && 
    		?ufoLong - 10 < ?metLong && ?metLong < ?ufoLong + 10).
}
